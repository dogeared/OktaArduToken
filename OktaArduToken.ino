#include <Arduboy2.h>
#include <swRTC.h>
#include <sha1.h>
#include <TOTP.h>
#include <Base32.h>
#include <EEPROM.h>

//#define DEBUG 1

#ifdef DEBUG
  #define DEBUG_PRINT(x) Serial.print (x)
  #define DEBUG_PRINTDEC(x) Serial.print (x, DEC)
  #define DEBUG_PRINTLN(x) Serial.println (x)
#else
  #define DEBUG_PRINT(x)
  #define DEBUG_PRINTDEC(x)
  #define DEBUG_PRINTLN(x)
#endif

const byte APP_ID = 0x20;
const int APP_ID_SAVE_ADDRESS = EEPROM_STORAGE_SPACE_START + 100;

Arduboy2 arduboy;
Base32 base32;
swRTC rtc;

String totpCode;
bool secretSet;
int secretPosition;
String secret;
String date;
bool dateSet;
int datePosition;

byte* hmacKey = NULL;
byte secretBuf[16];

const unsigned char PROGMEM oktadevlogo[] =
{
0x00, 0x00, 0xb8, 0x04, 0xfb, 0xfd, 0xfe, 0xff, 0x1f, 0xef, 0x2f, 0x2f, 0x2f, 0x2f, 0x00, 0x0f, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x80, 0x80, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xf0, 0x08, 0xf4, 0xfa, 0xfa, 0xfa, 0xfa, 0xf4, 0x08, 0xf0, 0x00, 0x00, 0x00, 0x80, 0x80, 0x80, 0x80, 0x80, 0xf0, 0x00, 0xf2, 0xfa, 0xfa, 0xfa, 0xf2, 0xe4, 0x18, 0xe0, 0x80, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x80, 0x80, 0x80, 0x00, 0x00, 0x00, 0x80, 0x80, 0x80, 0x80, 0x00, 0x00, 0x0e, 0x00, 0x0e, 0x2f, 0x2f, 0x0f, 0xef, 0x4f, 0x3f, 0xff, 0xfe, 0xfd, 0xf2, 0x0c, 0xf8, 0x00, 0x00, 
0xc0, 0x38, 0x82, 0xf8, 0xff, 0xff, 0xff, 0x7f, 0x80, 0x7f, 0x00, 0x00, 0x00, 0xc0, 0x20, 0x90, 0xe8, 0xf4, 0xfa, 0xf9, 0xfd, 0xfd, 0x7e, 0x7e, 0x7e, 0xfc, 0xfd, 0xfd, 0xfb, 0xfa, 0xf4, 0xe8, 0xbf, 0x00, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x80, 0xef, 0xf4, 0xfa, 0xfd, 0xfe, 0xfe, 0x7e, 0xbe, 0x5e, 0xed, 0x00, 0xff, 0xff, 0xff, 0xff, 0xff, 0x7e, 0x7e, 0x7e, 0x7e, 0x7e, 0x7d, 0x82, 0xcc, 0xf4, 0xfa, 0xfa, 0xfd, 0xfd, 0xfc, 0x7e, 0x7e, 0x7e, 0xfd, 0xfd, 0xfd, 0xfe, 0xfe, 0xfe, 0xfe, 0xfd, 0x02, 0xfc, 0x00, 0x00, 0x00, 0x00, 0x00, 0x1f, 0xe0, 0x00, 0xff, 0xff, 0xff, 0xff, 0xe0, 0x9f, 0x60, 0x80, 
0x36, 0xcf, 0x1f, 0xff, 0xff, 0xff, 0xf9, 0xe6, 0x19, 0xe0, 0x00, 0x00, 0x1e, 0x61, 0x8e, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xf1, 0xce, 0xd3, 0x81, 0xd1, 0xde, 0xc0, 0xff, 0xff, 0xff, 0xff, 0xff, 0x7f, 0x00, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x3f, 0x7f, 0xff, 0xff, 0xff, 0xf9, 0xf6, 0xeb, 0xd1, 0x20, 0x7f, 0x80, 0x7f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc1, 0xdf, 0xa1, 0xbf, 0x80, 0x3f, 0xff, 0xff, 0xff, 0xff, 0xff, 0xe0, 0xde, 0xd1, 0x81, 0xd3, 0xce, 0xe1, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xc0, 0xdf, 0x80, 0x00, 0x80, 0x00, 0x00, 0x80, 0x70, 0x09, 0xf6, 0xf9, 0xff, 0xff, 0x7f, 0x9f, 0x6f, 0x16, 
0x00, 0x01, 0xd4, 0x01, 0xff, 0xff, 0xff, 0xff, 0x80, 0x3f, 0x60, 0x20, 0x20, 0x00, 0x01, 0x02, 0x05, 0x0b, 0x17, 0x17, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x17, 0x1b, 0x09, 0x04, 0x0f, 0x10, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x10, 0x0f, 0x04, 0x09, 0x13, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2e, 0x13, 0x0c, 0x0b, 0x13, 0x17, 0x2f, 0x2f, 0x2f, 0x4f, 0x4f, 0x2f, 0x04, 0x10, 0x09, 0x0b, 0x17, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x2f, 0x17, 0x17, 0x17, 0x2f, 0x2f, 0x2f, 0x4f, 0x2f, 0x2f, 0x10, 0x2f, 0x20, 0x20, 0x7f, 0x20, 0xc0, 0xff, 0xff, 0xff, 0xff, 0x00, 0xff, 0x00, 0x00, 
0x00, 0x00, 0x01, 0x02, 0x0d, 0x0b, 0x07, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x00, 0x0f, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x07, 0x00, 0x07, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x0f, 0x07, 0x0b, 0x04, 0x03, 0x01, 0x00, 0x00, 
};

void drawLogoBitmap(int16_t y)
{
  arduboy.drawBitmap(14, y, oktadevlogo, 100, 36);
}

void begin() {
  arduboy.boot();
  arduboy.display();
  arduboy.flashlight();
  arduboy.systemButtons();
  arduboy.audio.begin();
  arduboy.bootLogoShell(drawLogoBitmap);
  arduboy.waitNoButtons();
}

void setup() {

  #ifdef DEBUG
    Serial.begin(9600);
    while (!Serial) {
      ;
    }
  #endif
  DEBUG_PRINTLN("setup()");

  secretSet = false;
  secretPosition = 0;
  //secret = "MMMMMMMMMMMMMMMM";
  secret = "C46MPM6ZTP3X6HQB";
  dateSet = false;
  datePosition = 0;
  
  rtc.stopRTC();
  rtc.setDate(17, 7, 2018);
  rtc.setTime(9, 00, 20);
  rtc.startRTC();

  int sec = rtc.getSeconds();
  int minu = rtc.getMinutes();
  int hours = rtc.getHours();
  int day = rtc.getDay();
  int mon = rtc.getMonth();
  int year = rtc.getYear();
  date = padNum(mon) + "/" + padNum(day) + "/" + padNum(year) + " " + padNum(hours) + ":" + padNum(minu) + ":" + padNum(sec);

  byte appId = EEPROM.read(APP_ID_SAVE_ADDRESS);
  if (appId != APP_ID) {
    DEBUG_PRINTLN("First time in. Writing APP_ID and settings...");
    EEPROM.write(APP_ID_SAVE_ADDRESS, APP_ID);
    //saveTotpInfo(totpInfo);
  } else {
    DEBUG_PRINTLN("Found APP_ID. Loading in settings...");
    //totpInfo = loadTotpInfo();
  }
  begin();
}

void loop() {
  if (!(arduboy.nextFrame())) return;

  arduboy.pollButtons();
  arduboy.clear();

  if (!secretSet) {
    setSecret();
  } else if (!dateSet) {
    setDate();
  } else if (secretSet && dateSet) {
    showTotpCode();
  }

  arduboy.display();
}

void printWithInvertChar(String str, int16_t x, int16_t y, int16_t charToInvert, int16_t size) {
  int color = 1;
  int bg = 0;
  for(int i = 0; str[i] != '\0'; i++) {
    if (charToInvert == i) {
      color = 0;
      bg = 1;
    } else {
      color = 1;
      bg = 0;
    }
    arduboy.drawChar(x + (6*i*size), y, str[i], color, bg, size);
  }
}

String padNum(int num) {
  if (num < 10) {
    return "0" + String(num);
  } else {
    return String(num);
  }
}

String updateSecret(String secret, int secretPosition, int addOrSub) {
  String ret = secret.substring(0, secretPosition);
  char upd = secret.charAt(secretPosition) + addOrSub;
  if (upd < '0') {
    upd = 'Z';
  } else if (upd > 'Z') {
    upd = '0';
  } else if (upd < 'A' and upd > '9' and addOrSub < 0) {
    upd = '9';
  } else if (upd > '9' and upd < 'A' and addOrSub > 0) {
    upd = 'A';
  }
  ret += upd + secret.substring(secretPosition + 1);
  return ret;
}

String updateDate(String date, int datePosition, int addOrSub) {
  String ret = date.substring(0, datePosition);
  char upd = date.charAt(datePosition) + addOrSub;
  if (upd < '0') {
    upd = '9';
  } else if (upd > '9') {
    upd = '0';
  }
  ret += upd + date.substring(datePosition + 1);
  return ret;
}

void setSecret() {
  // Okta like: C46MPM6ZTP3X6HQB
  arduboy.setCursor(0, 0);
  arduboy.print("Set OKTA Secret:");
  arduboy.setCursor(0, 30);
  arduboy.println("u/d: cycle letters/#s");
  arduboy.println("l/r: set position");
  arduboy.println("A: save secret");

  if (arduboy.justPressed(RIGHT_BUTTON)) {
    secretPosition++;
    if (secretPosition >= secret.length()) {
      secretPosition = 0;
    }
  }

  if (arduboy.justPressed(LEFT_BUTTON)) {
    secretPosition--;
    if (secretPosition < 0) {
      secretPosition = secret.length() - 1;
    }
  }

  if (arduboy.justPressed(DOWN_BUTTON)) {
    secret = updateSecret(secret, secretPosition, -1);  
  }

  if (arduboy.justPressed(UP_BUTTON)) {
    secret = updateSecret(secret, secretPosition, 1);
  }

  if (arduboy.justPressed(A_BUTTON)) {
    secret.getBytes(secretBuf, secret.length() + 1);
    base32.fromBase32(secretBuf, sizeof(secretBuf), (byte*&)hmacKey);

    // TODO - write settings to eeprom
    secretSet = true;
  }

  printWithInvertChar(secret, 0, 15, secretPosition, 1);
  
}

void setDate() {
  arduboy.setCursor(0, 0);
  arduboy.print("Set Date (GMT):");
  arduboy.setCursor(0, 30);
  arduboy.println("u/d: cycle #s");
  arduboy.println("l/r: set position");
  arduboy.println("A: save date");
  arduboy.println("B: set secret");

  if (arduboy.justPressed(RIGHT_BUTTON)) {
    datePosition++;
    if (datePosition == 2 or datePosition == 5 or datePosition == 10 or datePosition == 13 or datePosition == 16) {
      datePosition++;
    }
    if (datePosition >= date.length()) {
      datePosition = 0;
    }
  }

  if (arduboy.justPressed(LEFT_BUTTON)) {
    datePosition--;
    if (datePosition == 2 or datePosition == 5 or datePosition == 10 or datePosition == 13 or datePosition == 16) {
      datePosition--;
    }

    if (datePosition < 0) {
      datePosition = date.length() - 1;
    }
  }

  if (arduboy.justPressed(DOWN_BUTTON)) {
    date = updateDate(date, datePosition, -1);  
  }

  if (arduboy.justPressed(UP_BUTTON)) {
    date = updateDate(date, datePosition, 1);
  }

  if (arduboy.justPressed(B_BUTTON)) {
    secretSet = false;
  }

  if (arduboy.justPressed(A_BUTTON)) {
    int mm = date.substring(0,2).toInt();
    int dd = date.substring(3,5).toInt();
    int yyyy = date.substring(6,10).toInt();
    int HH = date.substring(11,13).toInt();
    int MM = date.substring(14,16).toInt();
    int SS = date.substring(17).toInt();

    rtc.stopRTC();
    rtc.setDate(dd, mm, yyyy);
    rtc.setTime(HH, MM, SS);
    rtc.startRTC();

    // TODO - write settings to eeprom
    dateSet = true;
  }

  printWithInvertChar(date, 0, 15, datePosition, 1);
}

void showTotpCode() {

  arduboy.setCursor(0, 45);
  arduboy.println("A: set date");
  arduboy.println("B: set secret");

  if (arduboy.justPressed(B_BUTTON)) {
    secretSet = false;
  }

  if (arduboy.justPressed(A_BUTTON)) {
    dateSet = false;
  }

  TOTP totp = TOTP(hmacKey, 10);
  
  long GMT = rtc.getTimestamp();
  totpCode = totp.getCode(GMT);
  printWithInvertChar(totpCode, 0, 10, -1, 2);

  int sec = rtc.getSeconds();
  int minu = rtc.getMinutes();
  int hours = rtc.getHours();
  int day = rtc.getDay();
  int mon = rtc.getMonth();
  int year = rtc.getYear();
  String dateStr = padNum(mon) + "/" + padNum(day) + "/" + padNum(year) + " " + padNum(hours) + ":" + padNum(minu) + ":" + padNum(sec);
  printWithInvertChar(dateStr, 0, 30, -1, 1);
}
